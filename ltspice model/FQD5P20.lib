*$
**************** Power Discrete MOSFET Electrical Circuit Model *****************
** Product Name: FQD5P20
** 200V P-Channel MOSFET and D-PAK
** Model Type: BSIM3V3
**-------------------------------------------------------------------------------
.SUBCKT FQD5P20 2 1 3
*Nom Temp=25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Lgate 1 9    1.125e-9
Ldrain 2 5   1.035e-9
Lsource 3 7  1.035e-9
RLgate 1 9   11.25
RLdrain 2 5  10.35
RLsource 3 7 10.35
Rgate 9 6 0.5
It 7 17 1
Ebreak 5 11 17 7 -220
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=8.6e-4 TC2=-1.05e-6)
.MODEL DbodyMOD D (IS=1.05e-17  N=1.0    RS=0.122     TRS1=1.0e-5   TRS2=1.1e-8
+ CJO=4.44e-10     M=0.56       VJ=0.42  TT=1.62e-7   XTI=3.0       EG=1.18)
.MODEL DbreakMOD D (RS=0.1 TRS1=1e-3 TRS2=1e-6)
Rdrain 5 16 RdrainMOD 1.1
.MODEL RdrainMOD RES (TC1=7.1e-3 TC2=1.0e-5)
M_BSIM3 16 6 7 7 Bsim3 W=0.98 L=2.0e-6 NRS=1
.MODEL Bsim3 PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 PARAMCHK=1 NQSMOD=0
+ TOX=780e-10      XJ=1.04e-6      NCH=1.08e17
+ U0=250           VSAT=1.0e5      DROUT=1.0
+ DELTA=0.1        PSCBE2=0        RSH=6.5e-3
+ VTH0=-4.60       VOFF=-0.1       NFACTOR=1.1     
+ LINT=6.05e-7     DLC=6.05e-7     CGSO=1.1e-15
+ CGSL=0           CGDO=5.02e-13   CGDL=4.65e-10
+ CJ=0             CF=0            CKAPPA=0.2
+ KT1=-1.28        KT2=0           UA1=2.05e-9
+ NJ=10            FC=0.5)
.ENDS
*$
******************* Power Discrete MOSFET Thermal Model *************************
** Package: D-PAK
**-------------------------------------------------------------------------------
.SUBCKT FQD5P20_THERMAL TH TL
CTHERM1 TH 6 6.15e-05
CTHERM2 6 5  2.14e-04
CTHERM3 5 4  1.42e-03
CTHERM4 4 3  6.46e-03
CTHERM5 3 2  1.53e-02
CTHERM6 2 TL 6.02e-02 
RTHERM1 TH 6 4.38e-03
RTHERM2 6 5  1.88e-02
RTHERM3 5 4  8.91e-02
RTHERM4 4 3  3.15e-01
RTHERM5 3 2  7.03e-01
RTHERM6 2 TL 1.65e+00
.ENDS FQD5P20_THERMAL
**-------------------------------------------------------------------------------
** Creation: Oct.-19-2016   Rev: 2.0
** ON Semiconductor

